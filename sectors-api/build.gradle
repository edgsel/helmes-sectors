import java.nio.file.Files
import java.time.Year

plugins {
    id "java"
    id "org.springframework.boot" version "4.0.2"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.openapi.generator" version "7.14.0"
}

group = "com.helmes"
version = "1.0.0"
description = "Test project for Helmes"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations.integrationTestImplementation.extendsFrom configurations.testImplementation
configurations.integrationTestRuntimeOnly.extendsFrom configurations.testRuntimeOnly

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-liquibase"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.0"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.0"
    implementation "io.jsonwebtoken:jjwt-api:0.13.0"
    compileOnly "org.projectlombok:lombok"
    runtimeOnly "org.postgresql:postgresql"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.13.0"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.13.0"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework.boot:spring-boot-webtestclient"
    testImplementation "org.springframework.boot:spring-boot-starter-data-jpa-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    integrationTestImplementation "org.testcontainers:postgresql:1.21.4"
    integrationTestImplementation "org.testcontainers:junit-jupiter:1.21.4"
}

tasks.register("createChangeDir") {
    group = "liquibase"
    description = "Creates an empty SQL file for liquibase migration"

    def currentYear = Year.now().value
    def projectId = project.id
    def changelogDir = projectDir.toPath().resolve("src/main/resources/db/changelog/${currentYear}")

    doLast {
        Files.createDirectories(changelogDir)

        def filename = "${System.currentTimeMillis()}-${projectId}.sql"
        def newFilePath = changelogDir.resolve(filename)

        Files.writeString(
            newFilePath,
            "-- liquibase formatted sql logicalFilePath:classpath:/db/changelog/${currentYear}/${filename}\n" +
                "-- changeset edgsel:${projectId}\n"
        )

        logger.quiet("Created a new file: $newFilePath")
    }
}

tasks.named("test") {
    useJUnitPlatform()
    jvmArgs "-XX:+EnableDynamicAgentLoading"
}

tasks.named('processIntegrationTestResources') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register("integrationTest", Test) {
    group = "verification"
    description = "Runs integration tests"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
    jvmArgs "-XX:+EnableDynamicAgentLoading"

    shouldRunAfter tasks.test
}

tasks.named("check") {
    dependsOn tasks.integrationTest
}
